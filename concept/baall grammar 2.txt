PROGRAM -> IMPORT STATEMENT EXPORT

# imports

IMPORT -> "use" _STRING_PRIMITIVE ";" IMPORT | _EPSILON

# code

STATEMENT -> ASSIGNMENT ";" STATEMENT | FUNCTION_CALL ";" STATEMENT | CONTROL_STRUCTURES STATEMENT | _EPSILON

ASSIGNMENT -> DECLARATION | REASSIGNMENT

DECLARATION -> TYPE ":" IDENTIFIER "=" ELEMENT_VALUE | TYPE ":" IDENTIFIER ":=" ELEMENT_VALUE

REASSIGNMENT -> IDENTIFIER SHORTHAND_OPERATOR ELEMENT_VALUE | INCREMENT_OPERATION

ELEMENT_VALUE -> STRUCT_CREATION | FUNCTION_CREATION | EXPRESSION

# EXPRESSION -> STRUCT_CREATION | FUNCTION_CREATION | EVALUABLE


ARRAY_LITERAL -> "[" "]" | "[" ARRAY_ELEMENT ARRAY_ELEMENT_LIST "]"

ARRAY_ELEMENT_LIST -> "," ARRAY_ELEMENT ARRAY_ELEMENT_LIST | _EPSILON

ARRAY_ELEMENT -> EXPRESSION


STRUCT_CREATION -> "(" STRUCT_FIELD STRUCT_FIELD_LIST ")"

STRUCT_FIELD_LIST -> "," STRUCT_FIELD STRUCT_FIELD_LIST | _EPSILON

STRUCT_FIELD -> TYPE ":" IDENTIFIER



STRUCT_INITIALIZATION -> "&" "(" FILLED_STRUCT_FIELD FILLED_STRUCT_FIELD_LIST ")"

FILLED_STRUCT_FIELD -> IDENTIFIER

FILLED_STRUCT_FIELD_LIST -> "," STRUCT_FILLED STRUCT_FILLED_LIST | _EPSILON



FUNCTION_CREATION -> "{" FUNCTION_BODY "}" | FUNCTION_PARAMETER FUNCTION_PARAMETER_LIST "{" FUNCTION_BODY "}"

FUNCTION_PARAMETER_LIST -> "," FUNCTION_PARAMETER FUNCTION_PARAMETER_LIST | _EPSILON

FUNCTION_PARAMETER -> TYPE ":" IDENTIFIER

FUNCTION_BODY -> STATEMENT "return" VALUE



# EVALUABLE -> "(" EVALUABLE ")" | VALUE OPERATION_SERIES | VALUE OPTIONAL_UNARY_OPERATION

# OPTIONAL_UNARY_OPERATION -> UNARY_OPERATOR | _EPSILON
 
# OPERATION_SERIES -> BINARY_OPERATOR VALUE OPERATION_SERIES | _EPSILON


EXPRESSION -> TERM OPTIONAL_OPERATION

TERM -> "(" EXPRESSION ")" | VALUE

OPTIONAL_OPERATION -> BINARY_OPERATOR EXPRESSION | _EPSILON



FUNCTION_CALL -> IDENTIFIER "(" ")" | IDENTIFIER "(" FUNCTION_ARGUMENT FUNCTION_ARGUMENT_LIST ")"

FUNCTION_ARGUMENT -> VALUE

FUNCTION_ARGUMENT_LIST -> "," FUNCTION_ARGUMENT FUNCTION_ARGUMENT_LIST | _EPSILON



# If-Else, For and While Loop

CONTROL_STRUCTURES -> CONDITIONAL | FOR_LOOP | WHILE_LOOP


CONDITIONAL -> EXPRESSION "?" "{" STATEMENT "}" ELSE_BLOCK

ELSE_BLOCK -> "\|" "{" STATEMENT "}" |  "\|" EXPRESSION "{" STATEMENT "}" ELSE_BLOCK


FOR_LOOP -> IDENTIFIER "=" EXPRESSION ".." EXPRESSION OPTIONAL_FOR_INCREMENTOR  "{" STATEMENT "}"

OPTIONAL_FOR_INCREMENTOR -> "::" EXPRESSION | _EPSILON


WHILE_LOOP -> "while" EXPRESSION "{" STATEMENT "}"


VALUE -> PRIMITIVE | ARRAY_LITERAL | IDENTIFIER | FUNCTION_CALL



INCREMENT_OPERATION -> UNARY_OPERATOR IDENTIFIER | IDENTIFIER UNARY_OPERATOR

PRIMITIVE -> "none" | _STRING_PRIMITIVE | _NUMBER_PRIMITIVE | _BOOLEAN_PRIMITIVE

BINARY_OPERATOR -> "+" | "-" | "*" | "/" | "%" | "&" | "\|" | "^"  | "<<" | ">>" | "==" | "<" | ">"

UNARY_OPERATOR -> "++" | "--"

SHORTHAND_OPERATOR -> "=" | ":=" | "+=" | "-=" | "*=" | "/=" | "&=" | "|=" | "^="

TYPE -> "string" | "number" | "boolean" | "struct" | "function" | IDENTIFIER

# EXPORTS

EXPORT -> "export" "{" IDENTIFIER ADDITIONAL_EXPORTED_ELEMENTS "}" | _EPSILON

ADDITIONAL_EXPORTED_ELEMENTS -> "," IDENTIFIER ADDITIONAL_EXPORTED_ELEMENTS | _EPSILON